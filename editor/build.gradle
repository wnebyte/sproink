import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'application'
}

def projectName = 'editor'
group 'com.github.wnebyte'
version '1.0'

mainClassName = "com.github.wnebyte.editor.Main"
sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext.lwjglVersion = "3.2.3"
project.ext.jomlVersion = "1.10.2"
project.ext.lwjglNatives = "natives-windows"
project.ext.imguiVersion = '1.86.0'
project.ext.box2DVersion = '2.2.1.1'
project.ext.toolingApiVersion = "7.4"

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
            includes = ["**/*.glsl", "**/*.png", "**/*.ttf", "**/*.ogg", "**/*.gradle", "**/*.java", "**/*.xml", "**/*.jar"]
        }
    }
    test {
        java {
            srcDirs = ["src/test/java"]
        }
        resources {
            srcDirs = ["src/test/resources"]
            includes = ["**/*.glsl", "**/*.png", "**/*.ttf", "**/*.ogg", "**/*.gradle", "**/*.java", "**/*.xml", "**/*.jar"]
        }
    }
}

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
    // lwjgl
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nfd"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"

    // imgui
    implementation "io.github.spair:imgui-java-app:$imguiVersion"

    // gson
    implementation "com.google.code.gson:gson:2.8.6"

    // jbox2d
    implementation name: 'jbox2d-library'

    // reflections
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'

    // jaxb
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'

    // gradle-tooling-api
    implementation "org.gradle:gradle-tooling-api:$toolingApiVersion"

    // sproink-api
    implementation project(':sproink')

    // sproink-util
    implementation project(':util')

    // junit
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar{
    manifest{
        attributes(
                "Name": projectName,
                "Implementation-Title": projectName,
                "Implementation-Version": project.version,
                "Class-Path": configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']}) " +
                        "${System.properties['java.vm.version']}"
        )
    }
    archiveBaseName.set(projectName)
}